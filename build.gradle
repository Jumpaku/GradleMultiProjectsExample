buildscript {
    ext.kotlin_version = "1.5.10"
    ext.junit4_version = "4.12"
    ext.hamcrest_version = "2.1"
    ext.jvm_terget = "1.8"
}

plugins {
    id('org.jetbrains.kotlin.jvm').version("$kotlin_version")
    id("maven-publish")
}

allprojects {

    apply(plugin: "kotlin")
    apply(plugin: "maven-publish")

    group = "jumpaku"
    version = "1.0.0"
    repositories {
        mavenCentral()
    }

    tasks.named("compileKotlin") {
        kotlinOptions.jvmTarget = jvm_terget
    }
    tasks.named("compileTestKotlin") {
        kotlinOptions.jvmTarget = jvm_terget
    }

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
        if (name.endsWith("-test")) {
            implementation("org.hamcrest:hamcrest:$hamcrest_version")
            implementation("junit:junit:$junit4_version")
        }
    }

    // Publishing
    /**
     * To run publish,  ./gradlew publish -Pargs="`user id of nexus.jumpaku.net` `password`"
     */
    publishing {
        def (repositoryUsername, repositoryPassword) = (project?.findProperty("args") as String)?.split(/\s+/) ?: []
        def repositoryUrl = "https://nexus.jumpaku.net/repository/" +
                ((version.endsWith("SNAPSHOT")) ? "maven-snapshots" : "maven-releases")
        repositories {
            maven {
                url = uri(repositoryUrl)
                credentials {
                    username = repositoryUsername ?: "UNKOWN"
                    password = repositoryPassword ?: "UNKOWN"
                }
            }
        }

        publications {
            create("release", MavenPublication) {
                from(components["kotlin"])
            }
        }
    }
}